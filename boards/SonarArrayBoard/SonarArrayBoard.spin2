{Object_Title_and_Purpose}

{ Version History }
{
  23-March-2025: Added support for Comm Port and Debug Port
  18-March-2025: Changed units to mm instead of cm for Sonars
}

CON { timing }
  _clkfreq = 160_000_000                                                  'Standard clock frequency = 160 MHz
  m_clkfreq = _clkfreq/1000
  FAST_LOOP_MSEC = 10 ' 100 Hz
  MEDIUM_LOOP_MSEC = 20 ' 50 Hz
  SLOW_LOOP_MSEC = 200 ' 5 Hz
  VERYSLOW_LOOP_MSEC = 1000 ' 1 Hz

CON { IO-Comms }

  RX_DEBUG     = 63
  TX_DEBUG     = 62
  RX_COMM      = 0
  TX_COMM      = 1

CON { Comms Constants }

  BAUDRATE_DEBUG = 115_200
  BAUDRATE_COMM  = 115_200

CON { Sonar Constants }
  PIN_SONAR1 = 46

CON { Other App Constants }
  MAX_NUMBER_SONARS = 20

VAR { timing }
  long fast_loop_counter
  long medium_loop_counter
  long slow_loop_counter
  long veryslow_loop_counter

VAR { Messaging }
  long sequence_number
  long start_timestamp

VAR { Sensor Variables }
  word fakedistance_cm
  word distance1_mm



OBJ
   comm_debug : "jm_fullduplexserial" 'ref: https://github.com/parallaxinc/propeller/tree/master/libraries/community/p2/All/jm_full_duplex_serial
   comm_board : "jm_fullduplexserial" 'ref: https://github.com/parallaxinc/propeller/tree/master/libraries/community/p2/All/jm_full_duplex_serial
   ping : "jm_ping"

PUB init()
  init_variables()
  setup_comms()
  main()

PUB main() | fast_loop_trigger, medium_loop_trigger,slow_loop_trigger,veryslow_loop_trigger
  fast_loop_trigger := medium_loop_trigger :=  slow_loop_trigger := veryslow_loop_trigger := 0
  repeat
    fast_loop_trigger++
    medium_loop_trigger++
    slow_loop_trigger++
    veryslow_loop_trigger++
    if(fast_loop_trigger >= FAST_LOOP_MSEC)
      run_fast_loop()
      fast_loop_trigger := 0
    if(medium_loop_trigger >= MEDIUM_LOOP_MSEC)
      run_medium_loop()
      medium_loop_trigger := 0
    if(slow_loop_trigger >= SLOW_LOOP_MSEC)
      run_slow_loop()
      slow_loop_trigger := 0
    if(veryslow_loop_trigger >= VERYSLOW_LOOP_MSEC)
      run_veryslow_loop()
      veryslow_loop_trigger := 0

    waitms(1)

PUB run_fast_loop()
  fast_loop_counter++





PUB run_medium_loop()
  'send_dummy_sonar_packet()
  read_sonars()
  send_sonar_packet()
  medium_loop_counter++


PUB run_slow_loop()
  slow_loop_counter++

PUB run_veryslow_loop()
  send_debug()

  veryslow_loop_counter++


PUB init_variables()
    fast_loop_counter := medium_loop_counter := slow_loop_counter := veryslow_loop_counter := 0
    sequence_number := 0
    start_timestamp := getct()/m_clkfreq
    fakedistance_cm := 0

PUB setup_comms()

    comm_debug.start(RX_DEBUG, TX_DEBUG, %0000, BAUDRATE_DEBUG)
    comm_board.start(RX_COMM, TX_COMM, %0000, BAUDRATE_COMM)
PUB send_sonar_packet()

  comm_board.str(string("$AA"))
  comm_board.fxdec(sequence_number,4)
  comm_board.fxdec(1,2) ' Only 1 Sonar supported currently
  comm_board.fxdec(distance1_mm,4)
  comm_board.str(string(10,13))
  if sequence_number >= 9999
    sequence_number := 0
  else
    sequence_number++


PUB send_dummy_sonar_packet()
  comm_board.str(string("$AA"))                               ' Packet Header
  comm_board.fxdec(sequence_number,4)                        ' Sequence Number
  comm_board.fxdec(max_number_sonars,2)                       ' Max Number of Sonars
  repeat MAX_NUMBER_SONARS
    comm_board.fxdec(fakedistance_cm,4)                                     ' Max Sonar Value


  comm_board.str(string(10,13))
  if sequence_number >= 9999
    sequence_number := 0
  else
    sequence_number++

  if fakedistance_cm >= 9999
    fakedistance_cm := 0
  else
    fakedistance_cm++

PUB send_debug()
  comm_debug.str(string("Fast:"))
  comm_debug.dec(fast_loop_counter)
  comm_debug.str(string(" Med: "))
  comm_debug.dec(medium_loop_counter)
  comm_debug.str(string(" Slow: "))
  comm_debug.dec(slow_loop_counter)
  comm_debug.str(string(" Very Slow: "))
  comm_debug.dec(veryslow_loop_counter)
  comm_debug.str(string(10,13))

PUB read_sonars()
   distance1_mm := ping.millimeters(PIN_SONAR1)